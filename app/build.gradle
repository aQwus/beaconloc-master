apply plugin: 'com.android.application'

android {
    compileSdkVersion 27
    buildToolsVersion "28.0.1"
    defaultConfig {
        applicationId 'com.samebits.beacon.locator'
        minSdkVersion 18
        targetSdkVersion 26
        versionCode 120
        versionName '1.2.0'
        testApplicationId 'com.samebits.beacon.locator.test'
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        abortOnError false
    }

    if (new File("signing.properties").exists()) {
        Properties signingProperties = new Properties()
        signingProperties.load(new FileInputStream(new File('signing.properties')))

        signingConfigs {
            release {
                storeFile new File(signingProperties['storeFile'])
                storePassword signingProperties['storePassword']
                keyAlias signingProperties['keyAlias']
                keyPassword signingProperties['keyPassword']
            }
        }

        buildTypes {
            release {
                signingConfig signingConfigs.release
            }
        }
    }
    productFlavors {
    }

    dataBinding {
        enabled = true
    }
}

dependencies {
    final SUPPORT_LIBRARY_VERSION = '27.1.1'
    final DAGGER_VERSION = '2.11'

    implementation fileTree(include: ['*.jar'], dir: 'libs')
    api 'org.altbeacon:android-beacon-library:2.15.1'
    implementation "com.android.support:support-v4:$SUPPORT_LIBRARY_VERSION"
    implementation "com.android.support:support-v13:$SUPPORT_LIBRARY_VERSION"
    implementation "com.android.support:appcompat-v7:$SUPPORT_LIBRARY_VERSION"
    implementation "com.android.support:preference-v7:$SUPPORT_LIBRARY_VERSION"
    implementation "com.android.support:cardview-v7:$SUPPORT_LIBRARY_VERSION"
    implementation "com.android.support:design:$SUPPORT_LIBRARY_VERSION"
    implementation "com.android.support:recyclerview-v7:$SUPPORT_LIBRARY_VERSION"
    implementation "com.android.support:support-annotations:$SUPPORT_LIBRARY_VERSION"
    implementation "com.google.dagger:dagger:$DAGGER_VERSION"
    annotationProcessor "com.google.dagger:dagger-compiler:$DAGGER_VERSION"
    compileOnly 'org.glassfish:javax.annotation:10.0-b28'
    implementation 'com.jakewharton:butterknife:8.8.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.0'

}
